use crate::{lexer, ast};
use lalrpop_util::ErrorRecovery;
use codespan::ByteOffset;

grammar<'err>(offset: ByteOffset, errors: &'err mut Vec<ErrorRecovery<usize, lexer::Tok, lexer::LexError>>);

extern {
    type Location = usize;
    type Error = lexer::LexError;

    enum lexer::Tok {
        "identifier" => lexer::Tok::Identifier(<String>),
        "string" => lexer::Tok::String(<String>),
        "number" => lexer::Tok::Number(<usize>),
        "type" => lexer::Tok::Type,
        "=" => lexer::Tok::Equals,
        "==" => lexer::Tok::DoubleEquals,
        "let" => lexer::Tok::Let,
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Asterisk,
        "/" => lexer::Tok::Slash,
        "{" => lexer::Tok::LBrace,
        "}" => lexer::Tok::RBrace,
        "(" => lexer::Tok::LParen,
        ")" => lexer::Tok::RParen,
        "[" => lexer::Tok::LBracket,
        "]" => lexer::Tok::RBracket,
        ":" => lexer::Tok::Colon,
        ";" => lexer::Tok::Semicolon,
        "," => lexer::Tok::Comma,
        "->" => lexer::Tok::ThinArrow,
        "=>" => lexer::Tok::ThickArrow,
        "fn" => lexer::Tok::Fn,
        "!" => lexer::Tok::Not,
        "!=" => lexer::Tok::NotEquals,
        "true" => lexer::Tok::True,
        "false" => lexer::Tok::False,
        "&&" => lexer::Tok::And,
        "|" => lexer::Tok::Pipe,
        "||" => lexer::Tok::Or,
        "_" => lexer::Tok::Underscore,
        "." => lexer::Tok::Period,
        ".." => lexer::Tok::DoublePeriod,
        "mut" => lexer::Tok::Mut,
        "if" => lexer::Tok::If,
        "else" => lexer::Tok::Else,
        "%" => lexer::Tok::Percent,
        "%{" => lexer::Tok::PercentLBrace,
        "for" => lexer::Tok::For,
        "in" => lexer::Tok::In,
        "while" => lexer::Tok::While,
        "break" => lexer::Tok::Break,
        "continue" => lexer::Tok::Continue,
    }
}

Spanned<T>: ast::Spanned<T> = {
    <l:@L> <t:T> <r:@R> => ast::Spanned::new(t, span!(l, r, offset))
};

Identifier: String = "identifier";

EnumParam: ast::Spanned<ast::EnumParam> = {
    Spanned<"identifier"> => <>.map(ast::EnumParam::Simple),
    <l:@L> "{" <ty_fields:(<TyField> ",")*> "}" <r:@R> => ast::Spanned::new(ast::EnumParam::Record(ty_fields),
        span!(l, r, offset)),
    <l:@L> "{" <ty_fields:(<TyField> ",")*> <last:TyField> "}" <r:@R> => {
        let mut ty_fields = ty_fields;
        ty_fields.push(last);
        ast::Spanned::new(ast::EnumParam::Record(ty_fields),
            span!(l, r, offset))
    },
};

EnumParams: Vec<ast::Spanned<ast::EnumParam>> = {
    "(" <args: (<EnumParam> ",")+> ")" => args,
    "(" <args: (<EnumParam> ",")*> <last:EnumParam> ")" => {
        let mut args = args;
        args.push(last);
        args
    },
};

EnumCase: ast::EnumCase = {
    <id:Spanned<"identifier">> <params:EnumParams?> => ast::EnumCase {
        id,
        params: if let Some(params) = params { params } else { vec![] }
    }
};

Ty: ast::Spanned<ast::Type> = {
    <l:@L> <cases: (<Spanned<EnumCase>> "|")*> <last:Spanned<EnumCase>> <r:@R> => {
        if cases.is_empty() {
            if last.t.params.is_empty() {
                ast::Spanned::new(ast::Type::Type(last.t.id.t), span!(l, r, offset))
            } else {
                ast::Spanned::new(ast::Type::Enum(vec![last]), span!(l, r, offset))
            }
        } else {
            let mut cases = cases;
            cases.push(last);
            ast::Spanned::new(ast::Type::Enum(cases), span!(l, r, offset))
        }
    },
    <l:@L> "{" <ty_fields:(<TyField> ",")*> "}" <r:@R> => ast::Spanned::new(ast::Type::Record(ty_fields), span!(l, r, offset)),
    <l:@L> "{" <ty_fields:(<TyField> ",")*> <last:TyField> "}" <r:@R> => {
        let mut ty_fields = ty_fields;
        ty_fields.push(last);
        ast::Spanned::new(ast::Type::Record(ty_fields), span!(l, r, offset))
    },
    <l:@L> "[" <ty:Ty> ";" <len:Spanned<"number">> "]" <r:@R> =>
        ast::Spanned::new(ast::Type::Array(Box::new(ty), len), span!(l, r, offset)),
};

TyField: ast::TypeField = {
    <id:Spanned<"identifier">> ":" <ty:Ty> => ast::TypeField { id, ty }
};

TypeDecl: ast::TypeDecl =
    "type" <type_id:Spanned<"identifier">> "=" <ty:Ty> =>
        ast::TypeDecl { type_id, ty };

_Let: ast::Let = {
   "let" <ml:@L> <mr:@R> <pattern:Spanned<"_">> <ty:(":" <Ty>)?> "=" <expr:Expr> => ast::Let {
        pattern: pattern.map(|_| ast::Pattern::Wildcard),
        mutable: ast::Spanned::new(false, span!(ml, mr, offset)),
        ty,
        expr,
    },
   "let" <ml:@L> <mutable:"mut"?> <mr:@R> <id:Spanned<"identifier">> <ty:(":" <Ty>)?> "=" <expr:Expr> => ast::Let {
        pattern: id.map(ast::Pattern::String),
        mutable: ast::Spanned::new(mutable.is_some(), span!(ml, mr, offset)),
        ty,
        expr,
    },
};

Assign = Spanned<_Assign>;

_Assign: ast::Assign = {
   <lval:LVal> "=" <expr:Expr> => ast::Assign { <> },
};

FnDecl: ast::FnDecl = {
    "fn" <id:Spanned<"identifier">> "(" <type_fields:(<TyField> ",")*> ")"
        <return_type:("->" <Ty>)?> "=" <body:Expr> ";" => {
        ast::FnDecl { id, type_fields, return_type, body }
    },
    "fn" <id:Spanned<"identifier">> "(" <type_fields:(<TyField> ",")*> <last:TyField> ")"
        <return_type:("->" <Ty>)?> "=" <body:Expr> ";" => {
        let mut type_fields = type_fields;
        type_fields.push(last);
        ast::FnDecl { id, type_fields, return_type, body }
    }
};

Decl: ast::Decl = Spanned<DeclType>;

DeclType: ast::DeclType = {
    <TypeDecl> ";" => ast::DeclType::Type(<>),
    <FnDecl> => ast::DeclType::Fn(<>),
    ! => {
        errors.push(<>);
        ast::DeclType::Error
    },
};

ExprTypeOrLetOrAssign: ast::Expr = {
    Range,
    Spanned<_Let> => {
        let span = <>.span;
        <>.map(|l| ast::ExprType::Let(Box::new(ast::Spanned::new(l, span))))
    },
    <l:@L> <expr:Assign> <r:@R> => ast::Expr::new(ast::ExprType::Assign(Box::new(expr)), span!(l, r, offset)),
}

Range: ast::Expr = {
    <l:Expr> ".." <r:Expr> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Range(Box::new(l), Box::new(r)), lspan.to(rspan))
    },
    Expr,
};

Expr: ast::Expr = {
    <l:Expr> "==" <r:OpExpr> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Compare(Box::new(l), ast::CompareOp::Equal, Box::new(r)),
            lspan.to(rspan))
    },
    OpExpr,
};

OpExpr: ast::Expr = {
    <l:OpExpr> "+" <r:Factor> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Arith(Box::new(l), ast::ArithOp::Add, Box::new(r)),
            lspan.to(rspan))
    },
    <l:OpExpr> "-" <r:Factor> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Arith(Box::new(l), ast::ArithOp::Sub, Box::new(r)),
            lspan.to(rspan))
    },
    <l:OpExpr> "||" <r:Factor> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Bool(Box::new(l), ast::BoolOp::Or, Box::new(r)),
            lspan.to(rspan))
    },
    Factor,
};

ExprTypeSeq: ast::ExprType = {
    "{" <l:@L> <r:@R> "}" => ast::ExprType::Seq(vec![ast::Expr::new(ast::ExprType::Unit, span!(l, r, offset))], true),
    "{" <exprs:(<ExprTypeOrLetOrAssign> ";")*> <last:ExprTypeOrLetOrAssign> <last_semi:";"?> "}" => {
        let mut exprs = exprs;
        exprs.push(last);
        ast::ExprType::Seq(exprs, last_semi.is_none())
    }
};

Factor: ast::Expr = {
    <l:Factor> "*" <r:TermOrNeg> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Arith(Box::new(l), ast::ArithOp::Mul, Box::new(r)),
            lspan.to(rspan))
    },
    <l:Factor> "/" <r:TermOrNeg> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Arith(Box::new(l), ast::ArithOp::Div, Box::new(r)), lspan.to(rspan))
    },
    <l:Factor> "&&" <r:TermOrNeg> => {
        let lspan = l.span;
        let rspan = r.span;
        ast::Expr::new(ast::ExprType::Bool(Box::new(l), ast::BoolOp::And, Box::new(r)), lspan.to(rspan))
    },
    TermOrNeg,
};

TermOrNeg: ast::Expr = {
    Term,
    <l:@L> "-" <term:Term> => {
        let rspan = term.span;
        ast::Expr::new(ast::ExprType::Neg(Box::new(term)), span!(l, rspan.end().0, offset))
    },
    <l:@L> "!" <term:Term> => {
        let rspan = term.span;
        ast::Expr::new(ast::ExprType::Not(Box::new(term)), span!(l, rspan.end().0, offset))
    },
};

Term: ast::Expr = {
    Spanned<"number"> => <>.map(ast::ExprType::Number),
    Spanned<"string"> => <>.map(ast::ExprType::String),
    Spanned<"true"> => <>.map(|_| ast::ExprType::BoolLiteral(true)),
    Spanned<"false"> => <>.map(|_| ast::ExprType::BoolLiteral(false)),
    Spanned<"continue"> => <>.map(|_| ast::ExprType::Continue),
    Spanned<"break"> => <>.map(|_| ast::ExprType::Break),
    Spanned<("(" <Expr> ")")> => <>.map(|s| s.t),
    Spanned<("(" ")")> => <>.map(|s| ast::ExprType::Unit),
    Spanned<LVal> => <>.map(|e| ast::ExprType::LVal(Box::new(e))),
    Spanned<FnCall> => <>.map(|e| ast::ExprType::FnCall(Box::new(e))),
    Spanned<Array> => <>.map(|e| ast::ExprType::Array(Box::new(e))),
    Spanned<Record> => <>.map(|e| ast::ExprType::Record(Box::new(e))),
    Spanned<If> => <>.map(|e| ast::ExprType::If(Box::new(e))),
    Spanned<For> => <>.map(|e| ast::ExprType::For(Box::new(e))),
    Spanned<While> => <>.map(|e| ast::ExprType::While(Box::new(e))),
    Spanned<ExprTypeSeq>,
};

LVal = Spanned<_LVal>;

_LVal: ast::LVal = {
    <"identifier"> => ast::LVal::Simple(<>),
    <val:LVal> "." <field:Spanned<"identifier">> => ast::LVal::Field(Box::new(val), field),
    <val:LVal> "[" <subscript:Expr> "]" => ast::LVal::Subscript(Box::new(val), subscript),
};

FnCall = Spanned<_FnCall>;

_FnCall: ast::FnCall = {
    <id:Spanned<"identifier">> "(" <args:(<Expr> ",")*> ")" => ast::FnCall { id, args },
    <id:Spanned<"identifier">> "(" <args:(<Expr> ",")*> <last:Expr> ")" => {
        let mut args = args;
        args.push(last);
        ast::FnCall { id, args }
    }
};

Record = Spanned<_Record>;

_Record: ast::Record = {
    <id:Spanned<"identifier">> "%{" <field_assigns:(<FieldAssign> ",")*> "}" => ast::Record { <> },
    <id:Spanned<"identifier">> "%{" <field_assigns:(<FieldAssign> ",")*> <last:FieldAssign> "}" => {
        let mut field_assigns = field_assigns;
        field_assigns.push(last);
        ast::Record {
            id,
            field_assigns,
        }
    }
};

FieldAssign: ast::FieldAssign = {
    <id:Spanned<"identifier">> ":" <expr:Expr> => ast::FieldAssign { <> }
};

Array = Spanned<_Array>;

_Array: ast::Array = {
    "[" <initial_value:Expr> ";" <len:Expr> "]" => ast::Array { <> }
};

If = Spanned<_If>;

_If: ast::If = {
    "if" <cond:Expr> <then_expr:Spanned<ExprTypeSeq>> => ast::If {
        cond,
        then_expr,
        else_expr: None,
    },
    "if" <cond:Expr> <then_expr:Spanned<ExprTypeSeq>> "else" <else_expr:Spanned<ExprTypeSeq>> =>
        ast::If { cond, then_expr, else_expr: Some(else_expr) },
    "if" <cond:Expr> <then_expr:Spanned<ExprTypeSeq>> "else" <else_if:Spanned<If>> => ast::If {
        cond,
        then_expr,
        else_expr: Some(else_if.map(|expr| ast::ExprType::If(Box::new(expr)))),
    },
};

For = Spanned<_For>;

_For: ast::For = {
    "for" <index:Spanned<"identifier">> "in" <range:Range> <body:Spanned<ExprTypeSeq>> =>
        ast::For { index, range, body }
};

While = Spanned<_While>;

_While: ast::While = {
    "while" <cond:Expr> <body:Spanned<ExprTypeSeq>> => ast::While { cond, body }
};

pub Program: Vec<ast::Decl> = <Decl*>;
